////////////////////////////////////////////////////////////////////
// Simplified version of mars rover mission.  
// Relaxations:
// - Single agent
// - Minerals are point locations
// - Agent's movement based on displacement and not power
// - Problem space is a grid world
//
// The goal here is to harvest as many high-value mineral
// as possible within the time constraints.
//
// The states for each rover are the x,y positions.
// The actions are movement in the x,y axes.
// A third action for each rover is mineral harvesting when possible.
//
////////////////////////////////////////////////////////////////////
domain mars_rover_science_mission {

    requirements = {
        constrained-state, 
        integer-valued, 
        reward-deterministic
    };

    types {
		rover : object;
		mineral : object;
	}; 

    pvariables {

        // rover constants
        MAX-STEP(rover): { non-fluent, int, default = 1 };                      // max distance rover can move in x and y direction for each time step
        MAX-X          : { non-fluent, int, default = 10 };                     // max distance in positive and negative x direction
        MAX-Y          : { non-fluent, int, default = 10 };                     // max distance in positive and negative y direction

        // minerals constants
        MINERAL-AREA(mineral) : { non-fluent, real, default = 0.2 };            // mineral harvesting area (included for rendering purposes)
        MINERAL-VALUE(mineral): { non-fluent, real, default = 1 };              // mineral harvesting value
        MINERAL-POS-X(mineral): { non-fluent, int, default = 0 };               // mineral x location
        MINERAL-POS-Y(mineral): { non-fluent, int, default = 0 };               // mineral y location

        // states
        pos-x(rover)    : { state-fluent, int, default = 0 };                   // rover x position
        pos-y(rover)    : { state-fluent, int, default = 0 };                   // rover y position
        mineral-harvested(mineral) : { state-fluent, bool, default = false };   // if a mineral has been mineral mineral harvested or not

        // actions
        move-x(rover)      : { action-fluent, int, default = 0 };               // force input in x direction
        move-y(rover)      : { action-fluent, int, default = 0 };               // force input in y direction
        harvest(rover)     : { action-fluent, bool, default = false };          // mineral harvesting action
    };

    cpfs {

        pos-x'(?d) = 
            if (abs[pos-x(?d) + move-x(?d)] <= MAX-X) 
                then pos-x(?d) + move-x(?d) 
            else if (pos-x(?d) < 0)     // If step size is too big, then the rover will be constrained by the world bound
                then -MAX-X
            else
                MAX-X;
        pos-y'(?d) = 
            if (abs[pos-y(?d) + move-y(?d)] <= MAX-Y) 
                then pos-y(?d) + move-y(?d) 
            else if (pos-y(?d) < 0)     // If step size is too big, then the rover will be constrained by the world bound
                then -MAX-Y
            else
                MAX-Y;

        mineral-harvested'(?m) = mineral-harvested(?m) |
                ~mineral-harvested(?m) ^ [exists_{?d : rover}
                    [pos-x(?d) == MINERAL-POS-X(?m) ^ pos-y(?d) == MINERAL-POS-Y(?m) ^ harvest(?d)]];

    };

    // R = -sum u-x^2 + u-y^2 + sum minerals harvested - sum harvest actions
    reward = [-sum_{?d : rover} [abs[move-x(?d)] + abs[move-y(?d)]]] +
             + [sum_{?m : mineral }[if (exists_{?d : rover}
                        [pos-x(?d) == MINERAL-POS-X(?m) ^ pos-y(?d) == MINERAL-POS-Y(?m)
						 ^ harvest(?d) ^ ~mineral-harvested(?m)])
                            then MINERAL-VALUE(?m)
                            else 0.0]]
             - [sum_{?d : rover} [harvest(?d)]];

    state-invariants {
        forall_{?m : mineral} [ MINERAL-POS-X(?m) < MAX-X ];
        forall_{?m : mineral} [ MINERAL-POS-X(?m) > -MAX-X ];
        forall_{?m : mineral} [ MINERAL-POS-Y(?m) < MAX-Y ];
        forall_{?m : mineral} [ MINERAL-POS-Y(?m) > -MAX-Y ];

        forall_{?d : rover} [ pos-x(?d) <= MAX-X ];
        forall_{?d : rover} [ pos-x(?d) >= -MAX-X ];
        forall_{?d : rover} [ pos-y(?d) <= MAX-Y ];
        forall_{?d : rover} [ pos-y(?d) >= -MAX-Y ];
    };

    action-preconditions {
        forall_{?d : rover} [ move-x(?d) >= -MAX-STEP(?d) ];
        forall_{?d : rover} [ move-x(?d) <= MAX-STEP(?d) ];
        forall_{?d : rover} [ move-y(?d) >= -MAX-STEP(?d) ];
        forall_{?d : rover} [ move-y(?d) <= MAX-STEP(?d) ];
    };

    termination {
        forall_{?m : mineral} [ mineral-harvested(?m) ];
    };
    
}
